/-
Copyright (c) 2017 Johannes Hölzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes Hölzl, Yury Kudryashov, Kim Morrison
-/
module

public import Mathlib.Algebra.MonoidAlgebra.Lift

/-!
# MonoidAlgebra.mapDomain

-/

@[expose] public section

assert_not_exists NonUnitalAlgHom AlgEquiv

open Function
open Finsupp hiding single mapDomain

noncomputable section

variable {F R S T M N O : Type*}

/-! ### Multiplicative monoids -/

namespace MonoidAlgebra
variable [Semiring R] [Semiring S] {f : M → N} {a : M} {r : R}

/-- Given a function `f : M → N` between magmas, return the corresponding map `R[M] → R[N]` obtained
by summing the coefficients along each fiber of `f`. -/
@[to_additive (attr := simps) /--
Given a function `f : M → N` between magmas, return the corresponding map `R[M] → R[N]` obtained
by summing the coefficients along each fiber of `f`. -/]
def mapDomain (f : M → N) (x : R[M]) : R[N] := .ofCoeff <| Finsupp.mapDomain f x.coeff

@[to_additive (attr := simp)]
lemma mapDomain_zero (f : M → N) : mapDomain f (0 : R[M]) = 0 := by ext; simp

@[to_additive]
lemma mapDomain_add (f : M → N) (x y : R[M]) :
    mapDomain f (x + y) = mapDomain f x + mapDomain f y := by
  ext; simp [Finsupp.mapDomain_add]

@[to_additive]
lemma mapDomain_sum (f : M → N) (x : S[M]) (v : M → S → R[M]) :
    mapDomain f (x.coeff.sum v) = x.coeff.sum fun a b ↦ mapDomain f (v a b) := by
  ext; simp [Finsupp.mapDomain_sum]

@[to_additive (relevant_arg := M) (attr := simp)]
lemma mapDomain_single : mapDomain f (single a r) = single (f a) r := by ext; simp

@[to_additive]
lemma mapDomain_injective (hf : Injective f) : Injective (mapDomain (R := R) f) :=
  ofCoeff_injective.comp <| (Finsupp.mapDomain_injective hf).comp coeff_injective

@[to_additive (dont_translate := R) (attr := simp) mapDomain_one]
theorem mapDomain_one [One M] [One N] {F : Type*} [FunLike F M N] [OneHomClass F M N] (f : F) :
    mapDomain f (1 : R[M]) = (1 : R[N]) := by
  simp [one_def]

section Mul
variable [Mul M] [Mul N] [Mul O] [FunLike F M N] [MulHomClass F M N]

@[to_additive (dont_translate := R) mapDomain_mul]
lemma mapDomain_mul (f : F) (x y : R[M]) : mapDomain f (x * y) = mapDomain f x * mapDomain f y := by
  simp [mul_def, mapDomain_sum, add_mul, mul_add, sum_mapDomain_index]

variable (R) in
/-- If `f : G → H` is a multiplicative homomorphism between two monoids, then
`MonoidAlgebra.mapDomain f` is a ring homomorphism between their monoid algebras.

See also `MulEquiv.monoidAlgebraCongrRight`. -/
@[to_additive (attr := simps) /--
If `f : G → H` is a multiplicative homomorphism between two additive monoids, then
`AddMonoidAlgebra.mapDomain f` is a ring homomorphism between their additive monoid algebras.

See also `AddEquiv.addMonoidAlgebraCongrRight`. -/]
def mapDomainNonUnitalRingHom (f : M →ₙ* N) : R[M] →ₙ+* R[N] where
  toFun := mapDomain f
  map_zero' := mapDomain_zero _
  map_add' := mapDomain_add _
  map_mul' := mapDomain_mul f

@[to_additive (dont_translate := R) (attr := simp)]
lemma mapDomainNonUnitalRingHom_id : mapDomainNonUnitalRingHom R (.id M) = .id R[M] := by ext; simp

@[to_additive (dont_translate := R) (attr := simp)]
lemma mapDomainNonUnitalRingHom_comp (f : N →ₙ* O) (g : M →ₙ* N) :
    mapDomainNonUnitalRingHom R (f.comp g) =
      (mapDomainNonUnitalRingHom R f).comp (mapDomainNonUnitalRingHom R g) := by
  ext; simp [Finsupp.mapDomain_comp]

end Mul

variable [Monoid M] [Monoid N] [Monoid O]

variable (R) in
/-- If `f : G → H` is a multiplicative homomorphism between two monoids, then
`MonoidAlgebra.mapDomain f` is a ring homomorphism between their monoid algebras.

See also `MulEquiv.monoidAlgebraCongrRight`. -/
@[to_additive (attr := simps) /--
If `f : G → H` is a multiplicative homomorphism between two additive monoids, then
`AddMonoidAlgebra.mapDomain f` is a ring homomorphism between their additive monoid algebras.

See also `AddEquiv.addMonoidAlgebraCongrRight`. -/]
def mapDomainRingHom (f : M →* N) : R[M] →+* R[N] where
  toFun := mapDomain f
  map_zero' := mapDomain_zero _
  map_add' := mapDomain_add _
  map_one' := mapDomain_one f
  map_mul' := mapDomain_mul f

attribute [local ext high] ringHom_ext

@[to_additive (dont_translate := R) (attr := simp)]
lemma mapDomainRingHom_id : mapDomainRingHom R (.id M) = .id R[M] := by ext <;> simp

@[to_additive (dont_translate := R) (attr := simp)]
lemma mapDomainRingHom_comp (f : N →* O) (g : M →* N) :
    mapDomainRingHom R (f.comp g) = (mapDomainRingHom R f).comp (mapDomainRingHom R g) := by
  ext <;> simp

@[to_additive (dont_translate := R S)]
lemma mapRangeRingHom_comp_mapDomainRingHom (f : R →+* S) (g : M →* N) :
    (mapRangeRingHom N f).comp (mapDomainRingHom R g) =
      (mapDomainRingHom S g).comp (mapRangeRingHom M f) := by aesop

end MonoidAlgebra

open scoped MonoidAlgebra

namespace Equiv
variable [Semiring R] [Mul M] [Mul N] [Mul O]

/-- Equivalent monoids have additively isomorphic monoid algebras.

`MonoidAlgebra.mapDomain` as an `AddEquiv`. -/
@[to_additive (dont_translate := R) (attr := simps apply symm_apply)
/-- Equivalent additive monoids have additively isomorphic additive monoid algebras.

`AddMonoidAlgebra.mapDomain` as an `AddEquiv`. -/]
def monoidAlgebraCongrRight (e : M ≃ N) : R[M] ≃+ R[N] where
  toFun x := x.mapDomain e
  invFun x := x.mapDomain e.symm
  left_inv x := by ext; simp
  right_inv x := by ext; simp
  map_add' x y := by ext; simp

@[simp] lemma symm_monoidAlgebraCongrRight (e : M ≃ N) :
    e.monoidAlgebraCongrRight.symm = e.symm.monoidAlgebraCongrRight (R := R) := rfl

@[simp] lemma monoidAlgebraCongrRight_trans (e₁ : M ≃ N) (e₂ : N ≃ O) :
    (e₁.trans e₂).monoidAlgebraCongrRight (R := R) =
      e₁.monoidAlgebraCongrRight.trans e₂.monoidAlgebraCongrRight := by
  ext; simp [Finsupp.mapDomain_comp]

end Equiv

namespace AddEquiv
variable [Semiring R] [Semiring S] [Semiring T] [Mul M]

/-- Additively isomorphic rings have additively isomorphic monoid algebras.

`Finsupp.mapRange` as an `AddEquiv`. -/
@[to_additive (dont_translate := R S) (attr := simps)
/-- Additively isomorphic rings have additively isomorphic additive monoid algebras.

`Finsupp.mapRange` as an `AddEquiv`. -/]
def monoidAlgebraCongrLeft (e : R ≃+ S) : R[M] ≃+ S[M] where
  toFun x := .ofCoeff <| .mapRange e e.map_zero x.coeff
  invFun x := .ofCoeff <| .mapRange e.symm e.symm.map_zero x.coeff
  left_inv x := by ext; simp
  right_inv x := by ext; simp
  map_add' x y := by ext; simp

@[simp] lemma symm_monoidAlgebraCongrLeft (e : R ≃+ S) :
    e.monoidAlgebraCongrLeft.symm = e.symm.monoidAlgebraCongrLeft (M := M) := rfl

@[simp] lemma monoidAlgebraCongrLeft_trans (e₁ : R ≃+ S) (e₂ : S ≃+ T) :
    (e₁.trans e₂).monoidAlgebraCongrLeft (M := M) =
      e₁.monoidAlgebraCongrLeft.trans e₂.monoidAlgebraCongrLeft := by ext; simp

end AddEquiv

namespace MulEquiv
variable [Semiring R] [Monoid M] [Monoid N] [Monoid O]

/-- Isomorphic monoids have isomorphic monoid algebras. -/
@[to_additive (dont_translate := R) (attr := simps! apply symm_apply)
/-- Isomorphic monoids have isomorphic additive monoid algebras. -/]
def monoidAlgebraCongrRight (e : M ≃* N) : R[M] ≃+* R[N] :=
  .ofRingHom (MonoidAlgebra.mapDomainRingHom R e) (MonoidAlgebra.mapDomainRingHom R e.symm)
    (by apply MonoidAlgebra.ringHom_ext <;> simp) (by apply MonoidAlgebra.ringHom_ext <;> simp)

lemma toRingHom_monoidAlgebraCongrRight (e : M ≃* N) :
    e.monoidAlgebraCongrRight.toRingHom = MonoidAlgebra.mapDomainRingHom R e := rfl

@[simp] lemma symm_monoidAlgebraCongrRight (e : M ≃* N) :
    e.monoidAlgebraCongrRight.symm = e.symm.monoidAlgebraCongrRight (R := R) := rfl

@[simp] lemma monoidAlgebraCongrRight_trans (e₁ : M ≃* N) (e₂ : N ≃* O) :
    (e₁.trans e₂).monoidAlgebraCongrRight (R := R) =
      e₁.monoidAlgebraCongrRight.trans e₂.monoidAlgebraCongrRight := by
  ext; simp [Finsupp.mapDomain_comp]

end MulEquiv

namespace RingEquiv
variable [Semiring R] [Semiring S] [Semiring T] [Monoid M]

/-- Isomorphic rings have isomorphic monoid algebras. -/
@[to_additive (dont_translate := R S) (attr := simps! apply symm_apply)
/-- Isomorphic rings have isomorphic additive monoid algebras. -/]
def monoidAlgebraCongrLeft (e : R ≃+* S) : R[M] ≃+* S[M] :=
  .ofRingHom (MonoidAlgebra.mapRangeRingHom M e) (MonoidAlgebra.mapRangeRingHom M e.symm)
    (by apply MonoidAlgebra.ringHom_ext <;> simp) (by apply MonoidAlgebra.ringHom_ext <;> simp)

lemma toRingHom_monoidAlgebraCongrLeft (e : R ≃+* S) :
    e.monoidAlgebraCongrLeft.toRingHom = MonoidAlgebra.mapRangeRingHom M e := rfl

@[simp] lemma symm_monoidAlgebraCongrLeft (e : R ≃+* S) :
    e.monoidAlgebraCongrLeft.symm = e.symm.monoidAlgebraCongrLeft (M := M) := rfl

@[simp] lemma monoidAlgebraCongrLeft_trans (e₁ : R ≃+* S) (e₂ : S ≃+* T) :
    (e₁.trans e₂).monoidAlgebraCongrLeft (M := M) =
      e₁.monoidAlgebraCongrLeft.trans e₂.monoidAlgebraCongrLeft := by ext; simp

end RingEquiv

/-!
#### Conversions between `AddMonoidAlgebra` and `MonoidAlgebra`

We have not defined `AddMonoidAlgebra k G = MonoidAlgebra k (Multiplicative G)`
because historically this caused problems;
since the changes that have made `nsmul` definitional, this would be possible,
but for now we just construct the ring isomorphisms using `RingEquiv.refl _`.
-/

variable (k G) in
/-- The equivalence between `AddMonoidAlgebra` and `MonoidAlgebra` in terms of
`Multiplicative` -/
protected def AddMonoidAlgebra.toMultiplicative [Semiring k] [Add G] :
    AddMonoidAlgebra k G ≃+* MonoidAlgebra k (Multiplicative G) where
  toFun x := .ofCoeff <| x.coeff.mapDomain .ofAdd
  invFun x := .ofCoeff <| x.coeff.mapDomain Multiplicative.toAdd
  left_inv x := by ext; simp
  right_inv x := by ext; simp
  map_add' x y := by simp [Finsupp.mapDomain_add]
  map_mul' x y := by
    classical
    ext
    simp [MonoidAlgebra.coeff_mul, AddMonoidAlgebra.coeff_mul, Finsupp.sum_mapDomain_index, add_mul,
      mul_add, ite_add_zero, Multiplicative.ext_iff]

variable (k G) in
/-- The equivalence between `MonoidAlgebra` and `AddMonoidAlgebra` in terms of `Additive` -/
protected def MonoidAlgebra.toAdditive [Semiring k] [Mul G] :
    MonoidAlgebra k G ≃+* AddMonoidAlgebra k (Additive G) where
  toFun x := .ofCoeff <| x.coeff.mapDomain .ofMul
  invFun x := .ofCoeff <| x.coeff.mapDomain Additive.toMul
  left_inv x := by ext; simp
  right_inv x := by ext; simp
  map_add' x y := by simp [Finsupp.mapDomain_add]
  map_mul' x y := by
    classical
    ext
    simp [MonoidAlgebra.coeff_mul, AddMonoidAlgebra.coeff_mul, Finsupp.sum_mapDomain_index, add_mul,
      mul_add, ite_add_zero, Additive.ext_iff]
